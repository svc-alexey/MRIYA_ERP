&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Настройки = ПолучитьПереченьНастроек();
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Каждого Настройка Из Настройки Цикл 
		
		Реквизит = Новый РеквизитФормы(Настройка.Имя, Новый ОписаниеТипов(Настройка.Тип),, Настройка.Заголовок, Истина);
		
		ДобавляемыеРеквизиты.Добавить(Реквизит);
		
	КонецЦикла;
		
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Для Каждого Реквизит Из ДобавляемыеРеквизиты Цикл 
		
		Элемент = IT_МодификацияКонфигурации.ДобавитьПоле(ЭтаФорма, Реквизит.Имя);
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПрограммы.Настройка КАК Настройка,
	|	НастройкиПрограммы.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.IT_НастройкиПрограммы КАК НастройкиПрограммы
	|ГДЕ
	|	НастройкиПрограммы.Настройка <> """"";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	Результат = РезультатЗапроса.Выбрать();
	
	Пока Результат.Следующий() Цикл 
		
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтаФорма, Результат.Настройка) Тогда 
			Продолжить;
		КонецЕсли;
		
		ЭтаФорма[Результат.Настройка] = Результат.Значение.Получить();	
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Очистить();
	
	Для Каждого Элемент Из Элементы Цикл 
		
		Если ТипЗнч(Элемент) <> Тип("ПолеФормы") Тогда 
			Продолжить;
		КонецЕсли;
		
		Настройка = Элемент.Имя;
		Значение  = ЭтаФорма[Настройка];
		
		Если Не ЗначениеЗаполнено(Значение) Тогда 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТекущийОбъект.Добавить();
		
		НоваяСтрока.Настройка = Настройка;
		НоваяСтрока.Значение  = Новый ХранилищеЗначения(Значение);
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Функция ПолучитьПереченьНастроек()

	Настройки = Новый Массив;
	
	ДобавитьНастройку("Минимальный срок обработки заказа на закупку, дней", "МинимальныйСрокОбработкиЗаказаНаЗакупку", "Число", Настройки);

	Возврат Настройки;
	
КонецФункции

&НаСервере
Процедура ДобавитьНастройку(Заголовок, Имя, Тип, Настройки)
	
	Настройка = Новый Структура("Заголовок, Имя, Тип", Заголовок, Имя, Новый Массив);
	
	Если ТипЗнч(Тип) = Тип("Строка") Тогда 
		Настройка.Тип.Добавить(Тип(Тип));
	Иначе
		Настройка.Тип.Добавить(Тип);
	КонецЕсли;
	
	Настройки.Добавить(Настройка);
	
КонецПроцедуры



	